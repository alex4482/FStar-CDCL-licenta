\chapter{Detalii de implementare}


\section{Despre FStar}

Limbajul F* este un limbaj de programare orientat pe demonstrații ce poate fi \newline folosit pentru uz general. Printre aspectele acestui limbaj se numără și faptul că \newline prezintă suport pentru programare funcționala. De asemenea, este proiectat să execute o verificare formală a codului, astfel orice programator poate să adauge funcțiilor \newline specificații sub formă de pre-condiții, post-condiții, invarianți, pentru a putea asigura corectitudinea codului.

\section{Aspecte importante ale implementarii}

Cele mai importante proprietăți pe care le respectă acest 'SAT solver' sunt \newline faptul că sunt acoperite toate posibilele atribuiri pentru o formula, corectitudinea implementării, siguranța că programul se va termina și va produce un rezultat.

\subsection{Acoperirea fiecărei soluții}

Faptul că algoritmul DPLL asigură încercarea fiecărei posibile atribuiri de variabile pentru formule în formă normală conjunctivă și în același timp garantează că orice atribuire ignorată este falsă, este demonstrat încă de la introducerea sa.

Singura optimizare a algoritmului DPLL care a fost implementată se leagă de \newline verificarea existenței unei clauze false la fiecare pas recursiv după etapa de \textit{'unit\_propagation'}. \newpage Mai exact, la fiecare pas există o listă de literali, de lungime cel puțin 1, care au fost \newline adăugați la soluție, dar acești literali noi nu schimbă valoarea clauzelor care nu conțin nici unul din literalii din listă.
 
Astfel, această optimizare se folosește de o matrice de apariție care, pentru fiecare literal din formulă, reține câte o listă cu toate clauzele în care apare respectivul literal.  \newline  Se intersectează clauzele în care apar toți acești literali asignați la pasul curent, iar  \newline  căutarea unei clauze false se face doar peste această mulțime, în loc de întreaga  \newline formulă.

\subsection{Corectitudinea programului}

Pentru verificarea specificațiilor introduse de programator, se folosește un SMT solver numit Z3 \footnote{https://en.wikipedia.org/wiki/Z3\_Theorem\_Prover}.
Cu toate acestea, unele condiții pot fi prea dificile de demonstrat într-un punct anume al programului, unul din motivele pentru care limbajul F* oferă la dispoziția programatorilor instrucțiuni ce simplifică misiunea de a demonstra și \newline verifică proprietăți și condiții scrise în program. Recurente in cadrul acestui proiect sunt structurile \textit{'Lemma'} si instrucțiunile \textit{'assert'} care ajută la demonstrarea unor proprietăți între diferite tipuri de date ce respectă anumite condiții, și ajută 'SMT solver-ul' spunându-i să demonstreze întâi proprietăți intermediare.

Exemplu 'lemma':
\begin{lstlisting}[
	basicstyle= \footnotesize, %or \small or \footnotesize etc.
	]
let rec lemma_values_from_clause_same_for_all_supra_set_of_t
(t : truth_assignment) (c : clause) : Lemma
(requires are_clause_vars_in_assignment t c)
(ensures 
	(forall (other_t : truth_assignment
		{forall (v : variable_info{L.mem v t}). 
			(L.mem v other_t)}).
		(L.mem true (get_values_from_clause t c) 
		= L.mem true (get_values_from_clause other_t c)))
)
= 
if length c = 1 
then ()
else lemma_values_from_clause_same_for_all_supra_set_of_t t (L.tl c)

\end{lstlisting}

\newpage

Această \textit{lemma} ajută la demonstrarea faptului că pentru orice clauză \textit{c}, ai cărei literali au o valoare asignată in mulțimea \textit{t}, atunci valoarea de adevăr a clauzei \textit{c} în corespondență cu mulțimea \textit{t} va fi egala cu valoarea de adevăr a lui \textit{c} în corespondență cu orice mulțime care este supra-set pentru \textit{t}.

Exemplu instrucțiune 'assert':
\begin{lstlisting}[]
assert(L.noRepeats ress);

\end{lstlisting}

Această simplă instrucțiune ajută a arăta faptul că la momentul verificării acestei instrucțiuni, lista 'ress' nu conține elemente duplicate.

Exemple asemănătoare se vor găsi și în secțiunile ce urmează.

După verificarea unui fișier '.fst' se va extrage, în fișiere tipice limbajului Ocaml, codul ce este folosit la formarea rezultatului, și este eliminat codul ce ajută la \newline specificații si demonstrarea unor proprietăți. Cu cât validările devin mai complexe, cu atât se poate observa diferența mare de linii de cod dintre fișierele '.fst' si cele '.ml'.

De exemplu, modulul 'DpllPropagation' conține multe instrucțiuni ce ajută la verificare, în total ajungând la $\approx$600 de linii fișierul, însă corespondentul modul Ocaml extras din cel FStar, are $\approx$100 linii, însemnând ca s-au eliminat cele $\approx$500 linii care contribuiau la verificarea formala a modulului.

\subsection{Terminarea programului}

Terminarea programului și producerea unui rezultat trebuie să fie o certitudine, indiferent de valoarea rezultatului. Limbajul FStar poate verifica, dacă este specificat acest lucru, dacă o funcție/un program se va termina și va produce un rezultat indiferent de parametrii primiți. De asemenea, poate verifica dacă programul/funcția ar produce același rezultat dacă ar fi apelata de mai multe ori cu aceiași parametrii.

Programatorului i se oferă posibilitatea de a adnota funcții folosind cuvântul cheie "Tot", pentru a asigura că la momentul compilării se va încercă demonstrarea faptului că fiecare funcție, mai ales cele recursive, se termină, produc un rezultat și mereu același rezultat pentru aceleași argumente. 

\newpage

Toate metodele din modulele ce ajută la formarea rezultatului algoritmului DPLL sunt 'funcții totale', fie folosind adnotarea 'Tot' în signatura funcției, fie prin absența oricărei adnotări, efectul 'Tot' fiind cel implicit. Succesul compilării programului înseamnă că acesta se termină și fiecare 'input' are asociat un singur rezultat. În cazul în care o formulă are mai multe soluții, modul în care a fost implementat algoritmul va decide care din acele soluții se va returna.


Acesta este un exemplu de declarație a unei funcții ce conține cuvântul cheie "Tot":

\begin{lstlisting}[	basicstyle= \footnotesize, 
	%or \small or \footnotesize etc
	]

let rec are_variables_in_truth_assignment
(vars: list nat_non_zero { 
	L.noRepeats vars 
	/\ length vars > 0 }
) 
(t: truth_assignment) 
: Tot 
(res : bool { 
	((all_variables_are_in_truth_assignment' vars t ) <==> res) }
) 
(decreases length vars)

\end{lstlisting}

Se observă faptul că metoda este recursivă datorită cuvântului cheie \textit{rec} și că este nevoie să se specifice modul în care dimensiunea parametrilor scade, de exemplu lungimea unei liste, astfel încât la un moment dat va ajunge într-un punct minim în care funcția nu se va mai apela pe sine, mai exact prin ultima linie prefixată de cuvântul cheie \textit{decreases}.

\section{Structurile de date folosite}

S-a ales proiectarea unor structuri de date și colecții simple, pentru care au fost implementate operații generice.

Conform definiției, o formulă este o mulțime de clauze, astfel implementarea are forma unei liste înlănțuite de clauze.

\begin{lstlisting}[]
	type formula = f : list clause 
	\end{lstlisting}

O clauză este o mulțime de literali, de aceea structura ei apare ca o listă înlănțuită de literali.

\begin{lstlisting}[]
	type clause = c : list literal { length  c > 0} 
\end{lstlisting}

Un literal reprezintă o variabilă a cărei valoare de adevăr poate fi negată,\newline  motiv pentru care tipul 'literal' este creat astfel, folosind 2 constructori, 'Var' pentru a evidenția ca nu se neagă valoarea de adevăr a variabilei, și 'NotVar' pentru a arăta negația variabilei.

\begin{lstlisting}[]
type literal = 
	| Var : a: nat_non_zero -> literal
	| NotVar : a: nat_non_zero -> literal
\end{lstlisting}

Doi literali, 'Var x' și 'NotVar x' sunt considerați a nu fi egali, însă au variabilele egale.
	
Variabilele sunt reprezentate folosind numere naturale nenule, deoarece un \newline format des întâlnit al datelor de intrare pentru această problemă și care s-a considerat la conceperea proiectului, reprezentau literalii folosind numere întregi nenule. Am ales literalii sa fie reprezentați prin doi constructori alături de numere întregi strict pozitive pentru a evita verificări repetate în cod cu privire la semnul unei variabile oarecare și pentru a se putea face ușor distincția dintre tipurile de literali.

\begin{lstlisting}[]
let nat_non_zero = x:int{x > 0}
\end{lstlisting}

Pentru matricea de apariție a clauzelor și literalilor, s-a folosit o structură de date intermediară care reprezintă o linie a matricei și este formată dintr-o pereche de două \newline elemente, un literal și o listă înlănțuită de clauze.

\begin{lstlisting}[	basicstyle= \footnotesize, 
	%or \small or \footnotesize etc
	]
type aux_occurence_vector = 
	{lit : literal ; clauses : list clause }

type occurence_vector = oc_v : aux_occurence_vector
	{length oc_v.clauses > 0 
	/\ (forall (c : clause{L.mem c oc_v.clauses}). 
		(L.mem oc_v.lit c))}
\end{lstlisting}

Condiția acestui tip de date este că lista 'clauses' trebuie să conțină toate clauzele în care apare literalul 'lit' din formula primită la începerea programului.

Matricea în sine este o listă înlănțuita de 'occurence-vector', și trebuie mereu să respecte condiția că nu există două elemente în listă, ale căror membri 'lit' să fie egali ca și valoare.

\newpage

\begin{lstlisting}[	basicstyle= \footnotesize, 
	%or \small or \footnotesize etc
	]
let rec occurence_matrix_condition 
	(oc_matrix : list occurence_vector) = 
	(   length oc_matrix > 0 
		==> 
	(forall (ocv : occurence_vector{L.mem ocv (L.tl oc_matrix)}). 
		(ocv.lit <> (L.hd oc_matrix).lit))
	)
	/\ (length oc_matrix > 0 ==> 
		occurence_matrix_condition (List.Tot.tl oc_matrix))

type occurrence_matrix = oc_matrix : list occurence_vector{
	occurence_matrix_condition oc_matrix 
}
\end{lstlisting}

Urmează un fragment în care se afișează condițiile care trebuie să fie respectate de formula 'f' și matricea de apariție a formulei notată in această funcție cu 'res'.

\begin{lstlisting}[	basicstyle= \footnotesize, 
%or \small or \footnotesize etc
]
	
	let get_occurence_matrix 
	(f : formula {length f > 0}) 
	: (res : occurrence_matrix{
		length res = length (get_lits_in_formula f)
		/\ (forall (l : literal{L.mem l (get_lits_in_formula f)}). 
			(is_lit_in_occurence_matrix res l))
		/\ (forall (oc_v : occurence_vector{L.mem oc_v res}). 	
			(L.mem oc_v.lit (get_lits_in_formula f)) )
		/\ (forall (oc_v : occurence_vector {L.mem oc_v res}). 
			(forall (c : clause {L.mem c oc_v.clauses}). 
				(L.mem c f)))
		/\ (forall (oc_v : occurence_vector {L.mem oc_v res}). 
			(forall (c : clause {
				L.mem c f /\ (L.mem c oc_v.clauses = false)}). 
				(L.mem oc_v.lit c = false)))})
	
\end{lstlisting}

Rezultatul funcției ce verifică satisfiabilitatea unei formule este în sine o structură de date simplă creată pentru această problemă, reprezentată prin doi constructori, unul pentru situația în care formula e nesatisfiabilă și unul care conține o combinație de variabile pozitive nenule și valori booleene ce reprezintă o soluție validă pentru formulă. 

\newpage
\begin{lstlisting}[]
type result =
	| NotSat
	| Sat : a: truth_assignment{ length a > 0} -> result

\end{lstlisting}

În această lucrare, se va referi la o combinație de variabile pozitive și valori \newline booleene prin termenul 'truth-assignment' prescurtat 't' sau 'tau'.

Tipul de dată 'truth\_assignment' reprezintă o listă înlănțuită de elemente, structuri formate din doi parametri, unul 'value' ce reprezintă valoarea unei variabile, și 'var' ce reprezintă în sine variabila.

\begin{lstlisting}[]
type variable_info = {value: bool; variable: nat_non_zero}

type truth_assignment = ta : list variable_info { ta_contition ta }
\end{lstlisting}

Acest 'truth\_assignment' trebuie să respecte condiția că nu există doua elemente în lista care să aibe aceeași valoare a membrului 'var'. Invariantul a fost scris astfel simplificând condiția, și anume ca membrul 'var' al elementului din capul listei trebuie să nu apară în oricare alt element din listă, iar sub-mulțimea 'tail\_t', formată din toate elementele lui 't' cu excepția capului listei, trebuie să respecte aceeași proprietate.

\begin{lstlisting}[]
let rec ta_contition (ta : list variable_info) = 
(forall (var : variable_info {List.Tot.mem var ta}). 
	(count_variables_occurrence ta var.variable = 1 ))
	/\ (length ta > 0 ==> ta_contition (List.Tot.tl ta))

\end{lstlisting}

Structurile de date au fost alese a fi simple în detrimentul eficienței vitezei de execuție a programului, pentru a putea analiza dificultatea și complexitatea menținerii corectitudinii în implementarea corectă algoritmului DPLL în limbajul F*. 



\section{Modulele ce alcătuiesc programul}

Separarea programului pe mai multe fișiere are scopul de a ușura \newline dezvoltarea și modificarea codului în etapa implementării. De asemenea, orice \newline schimbare într-un fișier rezultă in re-verificarea acestuia la momentul compilării,\newline proces care devine cu atât mai îndelungat cu cât fișierul prezintă mai multe funcții și demonstrații complexe.

\newpage

Astfel, modulele acestui program sunt: \newline
$\bullet$ DataTypes - fișier unde s-au definit toate structurile de date explicate anterior, \newline invariantul pentru cele ce aveau nevoie de unul, și anumite funcții ajutătoare pentru a manipula structurile de date tip colecții demonstrând in același timp corectitudinea \newline operațiilor efectuate pe colecțiile respective. 

Un exemplu de astfel de funcție ar fi metoda
\textit{'add\_var\_to\_truth\_assignment'}, al cărei simplu scop este de a adăuga un nou element în colecția \textit{'truth\_assignment'}, însa care trebuie sa poată respecta următoarea post\_condiție.

\begin{lstlisting}[	basicstyle= \footnotesize, 
	%or \small or \footnotesize etc
	]

let add_var_to_truth (t : truth_assignment) 
(var : variable_info {count_variables_occurrence t var.variable = 0}) 
: Tot 
(res : truth_assignment  {
is_variable_in_assignment res var.variable
/\ List.Tot.mem var res
/\ count_variables_occurrence res var.variable = 1
/\ (length res = length t + 1) 
/\ (forall (v : variable_info {
	List.Tot.mem v res /\ v <> var}). (List.Tot.mem v t))
/\ (forall (v : variable_info {
	(List.Tot.mem v t)}). ((List.Tot.mem v res)))
/\ (forall (v: variable_info {
	List.Tot.mem v res = false}). 
	((List.Tot.mem v t = false)))
/\ (forall (l : literal{ 
	is_variable_in_assignment t (get_literal_variable l) }). 
	(is_variable_in_assignment res (get_literal_variable l)
/\ get_literal_value t l = get_literal_value res l))  

\end{lstlisting}

 Pe scurt, susține că noua listă trebuie să conțină toate elementele din mulțimea veche, să aibe lungimea cu 1 mai mare decât lista inițială, iar toate variabilele prezente in lista inițială au aceeași valoare în ambele mulțimi.

$\bullet$ Modulul 'DataTypeUtils' conține un număr considerabil de metode ajutătoare, folosite in mai multe module ale proiectului si care trebuie deci sa fie disponibile într-un singur loc.\newpage Printre aceste metode apar:



\begin{itemize}
	\item  funcții de procesare a parametrilor primiți;
		
	ex: \textit{get\_clause\_value }- returnează valoarea de adevăr a unei clauze considerând un \textit{'truth\_assignment'} primit ca parametru;
	
	\item \textit{lemme} care ajută la asigurarea și demonstrarea corectitudinii programului;
	ex: \textit{lemma\_no\_vars\_in\_t\_outside\_f\_length\_compare} folosită pentru a arata că dacă un 'truth\_assignment' nu conține nici o variabilă care nu este prezentă in formula 'f', atunci sigur lungimea variabilei 't' este mai mică sau egală cu numărul variabilelor distincte ce apar in formula 'f';
	
	\begin{lstlisting}[	basicstyle= \footnotesize, 
		%or \small or \footnotesize etc
		]

let rec lemma_no_vars_in_t_outside_f_length_compare
(vars: list nat_non_zero) 
(t : truth_assignment) 
: Lemma
(requires 
	L.noRepeats vars 
	/\ (no_variables_outside_f_are_in_t' vars t)
)
(ensures length t <= length vars ) 
(decreases (length t)) = 
if length t = 0
then let l = length vars in ()
else let new_t = 
	(remove_variable_from_assignment t (L.hd t).variable) in
let new_vars = 
	(remove_var_from_list vars (L.hd t).variable) in
lemma_no_vars_in_t_outside_f_length_compare new_vars new_t

	\end{lstlisting}
	
	\item pre/post-condiții importante și relevante demonstrării corectitudinii, referențiate folosind variabile globale pentru a reduce cantitatea de cod repetat și pentru a putea generaliza funcțiile la nivel înalt, având posibilitatea în viitor de a \newline optimiza aspecte ale programului precum structurile de date, pentru care ar \newline trebui modificate doar implementarea metodelor ce procesează aceste structuri;
	
	ex: \textit{t\_cant\_be\_solution\_for\_f}, folosit ca post-condiție care enunță ca orice 'truth\_assignment' ce conține toate variabilele formulei 'f' si pentru care parametrul 't' este o sub-mulțime a sa, nu este o soluție valida pentru 'f';
	
	\textit{t\_cant\_be\_solution\_for\_f}:
	
	\begin{lstlisting}
 (forall (whole_t: truth_assignment{ 
	t1_is_sublist_of_t2 t whole_t
	/\ are_variables_in_truth_assignment f whole_t 
	/\ length whole_t 
		=  length ( get_vars_in_formula f)}). 
	(is_solution f whole_t = false)) 
	\end{lstlisting}
	
	
	\item predicate, funcții care evaluează dacă anumiți parametrii respectă o anumită proprietate
	
	ex: \textit{is\_solution}, deși simplu, este unul din cei mai importanți predicați ai \newline programului; acesta susține că dacă un \textit{'tau'} conține toate variabilele din formula 'f', atunci este soluție doar dacă este soluție parțială, adică dacă nu conține clauze false.
	
	\begin{lstlisting}
let is_solution 
(f: formula  { length f > 0 })
(t: truth_assignment {
	((length (get_vars_in_formula f)) = length t)
	/\ all_variables_are_in_truth_assignment f t  
})
= is_partial_solution f t 
	\end{lstlisting}

\end{itemize}


$\bullet$ Modulul DpllPropagation conține puține metode însă importante pentru demonstrație, dar dificile de specificat și verificat. Aceste funcții se ocupă cu pasul de propagare a clauzelor 'unit' din algoritmul DPLL.

O mica parte, însă cea mai importantă din specificarea metodei principale este:

\begin{lstlisting}[]
	t1_is_sublist_of_t2 t (fst res)
	/\ ((t_cant_be_solution_for_f f t) <==> (    
		t_cant_be_solution_for_f f (fst res)) )
 	/\ length res._1 >= length t

\end{lstlisting}
	
Post-condițiile enunță următoarele: 
\begin{itemize}
	\item[$\diamond$]  faptul că tot ce e inclus in 'tau' primit ca parametru trebuie sa fie existent și in 'tau' trimis ca rezultat; 
	
	\item[$\diamond$] lungimea 'tau'-ului rezultat trebuie sa fie măcar egală cu lungimea 'tau'-ului primit, lucru care ajută la asigurarea terminării programului, fiindcă soluția finala trebuie sa aibă atâtea elemente câte variabile unice sunt în formulă;
	
	\item[$\diamond$] 'tau'-ul primit ca parametru nu este soluție pentru formula 'f' dacă și numai dacă 'tau'-ul rezultat este soluție
\end{itemize}

$\bullet$ Modulul 'OccurenceMatrix' conține funcții necesare creării, procesării și accesării matricei de apariție a literalilor în clauzele formulei, și de asemenea implementarea optimizată pentru metoda ce verifică dacă un '\textit{tau}' oarecare este soluție parțială pentru formula dată.

$\bullet$ Modulul 'Dpll' conține funcția principală ce primește o formulă și oferă un \newline rezultat, unde se specifică și verifică legătura dintre valoarea rezultatului și formulă.

\begin{lstlisting}[
	basicstyle= \footnotesize, %or \small or \footnotesize etc.
	]

: Tot (res: result {
(Sat? res = true ==> 
	((length (get_truth_from_result res) = 	length ( get_vars_in_formula f)) 
	/\ (all_variables_are_in_truth_assignment f (get_truth_from_result res)) 
/\ ( is_solution f (get_truth_from_result res) = true)))
/\ ((NotSat? res = true) ==> 
	( forall (whole_t: truth_assignment{ 
		(forall (v : variable_info { (List.Tot.mem v pre_dpll_t)}). 
			((List.Tot.mem v whole_t)))
		/\ are_variables_in_truth_assignment f whole_t 
		/\ length whole_t =  length ( get_vars_in_formula f)}). 
	is_solution f whole_t = false))
})  

\end{lstlisting}

$\bullet$ Modulul 'InputFileParser' a fost conceput pentru a putea folosi formule diverse de diferite dimensiuni prin parsarea unor fișiere de intrare ce respectă un anumit format. \footnote{https://people.sc.fsu.edu/~jburkardt/data/cnf/cnf.html}

 $\bullet$ Modulul 'ConvertorToString' este folosit pentru a converti tipurile de date create pentru acest proiect în șiruri de caractere, și mai ales pentru a se afișa rezultatul pe ecran sub o forma ușoara de citit și înțeles.


\section{Metrici orientative}

Această secțiune conține metrici aproximative sub forma unor timpi ce reprezintă durata de compilare/verificare a fișierelor, sau timpul necesar obținerii rezultatului 'Satisfiabil' sau 'Nesatisfiabil'.

Algoritmul fiind unul simplu în comparație cu optimizări moderne, timpul de demonstrare că o formula este nesatisfiabilă ajunge să fie considerabil de mare chiar și pentru date de intrare cu dimensiuni relativ mici, motiv pentru care sunt necesare îmbunătățiri ale algoritmului pentru a ajunge la o eficiență comparabilă.

Compilatorul a avut nevoie de aproximativ 500 de secunde pentru verificarea fișierelor '.fst' și extragerea fișierelor '.ml' specifice limbajului Ocaml. 
\newline
\begin{center}

\begin{tabular}{|l|l|l|} \hline
	Nume fișier & Durata verificare/compilare & Nr. linii .fst / Nr. linii .ml \\\hline
	DataTypes & 8 secunde & 166 : 106 \\\hline
	DataTypeUtils & 33 secunde & 760 : 297 \\\hline
	DpllPropagation & 285 secunde & 612 : 118 \\\hline
	OccurenceMatrixUtils & 91 secunde & 493 : 142 \\\hline
	Dpll & 71 secunde & 195 : 61 \\\hline
	ConvertorToString & 3 secunde & \_\\\hline
	InputFileParser & 4 secunde & \_ \\\hline
	Main & 3 secunde & 30 : 17 \\\hline	
\end{tabular}
\end{center}

Tabelul următor ilustrează timpii necesari obținerii unor rezultate.
\begin{center}
\begin{tabular}{|l|l|l|} \hline
	Nume fișier input & Tip rezultat & Durata obținerii rezultatului \\\hline
	hole6\_cnf & UNSAT & 23 secunde \\\hline
	aim-50\_cnf & SAT & $<$ 0.1 secunde \\\hline
	aim-100\_cnf & UNSAT & câteva minute \\\hline
	quinn\_cnf & SAT & $<$ 0.1 secunde \\\hline

\end{tabular}
\end{center}

Analizând codul sursă sau tabelul anterior, se observă că fișierele ce conțin mai multe linii de cod pentru procesarea datelor și mai puține ce necesită demonstrarea unor proprietăți simple, au nevoie de mai puțin timp pentru a se verifica și compila. În schimb, fișierele care au foarte puține linii de cod ce contribuie la formarea rezultatului și care ajung să fie extrase în fișierele '.ml', dar multe proprietăți mai complexe ale căror demonstrație trebuie verificată, necesită mult mai mult timp de compilare.
\newpage

Dispozitivul pe care s-a făcut această contorizare avea următoarele specificații:
\begin{itemize}
	\item cpu - intel-core i5-1035G1
	\item memorie ram - 16gb
	\item tip hard-disk stocare - ssd
\end{itemize}



\section{Posibile optimizări}

Mulți 'SAT solveri' moderi implementează algoritmul CDCL (Conflict-Driven-Clause-Learning) \footnote{https://www.cs.princeton.edu/~zkincaid/courses/fall18/readings/SATHandbook-CDCL.pdf}
 care, pe lângă pașii din algoritmul DPLL, adaugă un pas nou de învățare și eficientizează saltul înapoi în momentul în care se găsește o soluție falsă.

Un alt rafinament pe lângă îmbunătățirea algoritmului implementat poate fi eficientizarea structurilor de date folosite, pentru a reduce complexitatea timp a operațiilor ce se repetă cel mai des.

O problemă ce poate apărea și în implementarea algoritmul DPLL dar și cea a algoritmului mai complex CDCL, este o metoda/euristica ineficientă de a alege o nouă variabilă pentru a i se asigna o valoare, și însăși valoarea care să i se asocieze. Un exemplu ar fi metoda VSIDS 
\footnote{https://mk.cs.msu.ru/images/1/1f/SAT\_SMT\_Vijay\_Ganesh\_HVC2015.pdf}, care se bazează pe asocierea unui scor pentru fiecare variabilă, scor care se schimba dacă variabila apare într-un conflict. 

O alta optimizare ce se poate implementa este paralelizarea calculelor și împărțirea ramurilor/soluțiilor ce trebuie explorate.

O mică îmbunătățire, din perspectiva limbajului de programare de aceasta dată, poate fi detectarea instrucțiunilor ce au rolul de a ajuta demonstrarea unor proprietăți însă ajung pe parcursul dezvoltării programului să devină redundante, iar eliminarea lor ar putea reduce puțin timpii de validare/compilare.

De asemenea, mai exista varianta de a simplifica problema pe care algoritmul trebuie să o rezolve, iar analiza fiecărei clauze în parte, poate duce la identificarea unor clauze și literali redundanți, care pot fi eliminați din formula primită fără a schimba corectitudinea rezultatului final.